plugins {
	id 'org.asciidoctor.convert' version '1.5.3'
	id 'org.springframework.boot' version '2.1.7.RELEASE'
	id 'java'
	id "nebula.facet" version "6.0.3"
}

apply plugin: 'io.spring.dependency-management'

group = 'org.manuel'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '11'

facets {
	restDocTest {
		parentSourceSet = 'main'
		testTaskName = 'restDocTest'
		includeInCheckLifecycle = true
	}
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {

	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	compileOnly 'org.projectlombok:lombok:1.18.8'
	annotationProcessor 'org.projectlombok:lombok:1.18.8'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'


	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		exclude group: 'junit', module: 'junit'
	}
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
	testImplementation 'org.json:json:20180813'
	testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.1.9'
	testImplementation 'org.mockito:mockito-core'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'

	restDocTestImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		exclude group: 'junit', module: 'junit'
	}
	restDocTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
	restDocTestImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	restDocTestImplementation 'org.springframework.security:spring-security-test'
	restDocTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
}

test {
	useJUnitPlatform()
}

restDocTest {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn restDocTest
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}
