plugins {
	id 'org.asciidoctor.convert' version '1.5.3'
	id 'org.springframework.boot' version '2.0.0.BUILD-SNAPSHOT'
	id 'java'
	id "nebula.facet" version "6.0.3"
}

apply plugin: 'io.spring.dependency-management'

group = 'org.manuel'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '11'

facets {
	integrationTest {
		parentSourceSet = 'main'
		testTaskName = 'integrationTest'
		includeInCheckLifecycle = true
	}
	restDocTest {
		parentSourceSet = 'integrationTest'
		testTaskName = 'restDocTest'
		includeInCheckLifecycle = true
	}
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {

	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
	implementation 'org.hibernate.validator:hibernate-validator:6.1.0.Alpha6'
	compileOnly 'org.projectlombok:lombok:1.18.8'
	annotationProcessor 'org.projectlombok:lombok:1.18.8'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
	testImplementation 'org.json:json:20180813'
	testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.1.9'
	testImplementation 'org.mockito:mockito-junit-jupiter:3.0.0'
	testImplementation 'org.mockito:mockito-core'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'

	integrationTestImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		exclude group: 'junit', module: 'junit'
	}
	integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
	integrationTestImplementation 'org.springframework.security:spring-security-test'
	integrationTestImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
	integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'

	restDocTestImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	restDocTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'

}

test {
	useJUnitPlatform()
}

integrationTest {
	useJUnitPlatform()
}

restDocTest {
	outputs.dir snippetsDir
	useJUnitPlatform()
	dependsOn integrationTest
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn restDocTest
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}
