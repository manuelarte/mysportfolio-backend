import io.github.manuelarte.spring.ProjectVersion

plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id "nebula.facet" version "7.0.9"
    id 'com.google.cloud.tools.jib' version '2.6.0'
    id 'com.palantir.git-version' version '0.12.3'
    id 'checkstyle'
}

ext.versionFile = file('version.properties')
ext.gitDetails = versionDetails()

ProjectVersion readVersion() {
    logger.quiet 'Reading version file.'
    ProjectVersion.readVersion(versionFile, gitDetails)
}

group = 'org.manuel'
version = readVersion()
sourceCompatibility = '13'

facets {
    integrationTest {
        parentSourceSet = 'main'
        testTaskName = 'integrationTest'
        includeInCheckLifecycle = true
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url  "https://dl.bintray.com/manueldoncelmartos/mysportfolio" }

}

ext.mysportfolioVersion = "0.0.1-alpha1"

dependencies {
    implementation 'io.github.manuelarte.mysportfolio:mysportfolio-exceptions:0.0.1'
    implementation "io.github.manuelarte.mysportfolio:mysportfolio-model-constants:${mysportfolioVersion}"
    implementation "io.github.manuelarte.mysportfolio:mysportfolio-model:${mysportfolioVersion}"
    implementation "io.github.manuelarte.mysportfolio:mysportfolio-dtos:${mysportfolioVersion}"

    implementation 'javax.inject:javax.inject:1'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.threeten:threeten-extra:1.5.0'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.cloud:spring-cloud-gcp-starter-logging:1.2.6.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'org.aspectj:aspectjrt:1.9.6'
    implementation 'org.aspectj:aspectjweaver:1.9.6'

    implementation 'org.hibernate.validator:hibernate-validator:6.1.5.Final'
    implementation 'com.google.api-client:google-api-client:1.30.7'
    implementation 'com.google.api-client:google-api-client-jackson2:1.30.7'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'io.github.manuelarte.spring:manuelarte-validation:0.0.2'
    implementation 'io.github.manuelarte.spring:query-parameter-model:0.0.2'
    implementation 'io.github.manuelarte.spring:query-parameter-mongo:0.0.2'
    implementation 'org.jetbrains:annotations:20.1.0'
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    implementation 'com.google.firebase:firebase-admin:7.0.1'

    testImplementation 'org.json:json:20201115'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.6.28'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.5'
    testImplementation 'org.mutabilitydetector:MutabilityDetector:0.10.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    integrationTestImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    integrationTestImplementation 'org.springframework.security:spring-security-test'
    integrationTestImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'

}

test {
    useJUnitPlatform()
}

integrationTest {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

String getDockerTag() {
    gitDetails = ext.gitDetails
    if (gitDetails.branchName != "master") {
        return "${version}-${gitDetails.gitHash}"
    }
    return "${version}"
}

tasks.register("printDockerTag") {
    doLast {
        println getDockerTag()
    }
}

jib {
    from {
        image = 'openjdk:15'
    }
    to {
        image = "gcr.io/mysportfolio-backend/$name:${getDockerTag()}"
    }
    container {
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
}